#!/usr/bin/env zsh

# Search shell history with fzf and atuin
# Ref: https://github.com/atuinsh/atuin/issues/68

atuin-setup() {
  if ! which atuin &> /dev/null; then return 1; fi
  # bind default atuin to opt-e
  bindkey '^[e' atuin-search

  eval "$(atuin init zsh --disable-ctrl-r)"

  fzf-atuin-history-widget() {
    local selected num
    setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2>/dev/null

    # Searches max of 5000 commands by default; set ATUIN_LIMIT to change
    # local atuin_opts="--cmd-only --limit ${ATUIN_LIMIT:-5000}"
    local atuin_opts="--cmd-only"
    local fzf_opts=(
      --height=${FZF_TMUX_HEIGHT:-80%}
      --tac
      "-n2..,.."
      --tiebreak=index
      "--query=${LBUFFER}"
      "+m"
      "--bind=alt-up:half-page-up,alt-down:half-page-down"
      '--preview=echo {}'
      "--preview-window=down:3:hidden:wrap"
      "--bind=?:toggle-preview"
      "--bind=ctrl-d:reload(atuin search $atuin_opts -c $PWD),ctrl-r:reload(atuin search $atuin_opts)"
    )

    selected=$(
    eval "atuin search ${atuin_opts}" |
      fzf "${fzf_opts[@]}"
    )
    local ret=$?
    if [ -n "$selected" ]; then
      LBUFFER="${selected}" # or use += to insert at current cursor
    fi
    zle reset-prompt
    return $ret
  }
  # load and bind widget to ctrl-r
  zle -N fzf-atuin-history-widget
  bindkey '^R' fzf-atuin-history-widget
}
atuin-setup
